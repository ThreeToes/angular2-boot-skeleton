buildscript {
	ext {
		springBootVersion = '1.4.2.RELEASE'
	}
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath('se.transmode.gradle:gradle-docker:1.2')
		classpath('com.moowork.gradle:gradle-node-plugin:0.13')
		classpath('com.moowork.gradle:gradle-gulp-plugin:0.13')
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'docker'
apply plugin: 'com.moowork.gulp'

// run npm install
gulp_default.dependsOn 'npmInstall'

// run gulp install
gulp_default.dependsOn 'installGulp'

processResources.dependsOn gulp_default

jar {
	baseName = 'skeleton-boot-angular2'
	version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-actuator')
	runtime('org.springframework.boot:spring-boot-devtools')
	compileOnly('org.projectlombok:lombok')
	testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('info.cukes:cucumber-java:1.2.4')
    testCompile('info.cukes:cucumber-junit:1.2.4')
    testCompile('info.cukes:cucumber-spring:1.2.4')
	testCompile('org.seleniumhq.selenium:selenium-java:3.0.1')
}

task buildDocker(type: Docker, dependsOn: build) {
	push = false
	applicationName = jar.baseName
	dockerfile = file('src/main/docker/Dockerfile')
	doFirst {
		copy {
			from jar
			into stageDir
		}
	}
}
